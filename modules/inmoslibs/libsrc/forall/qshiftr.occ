--
--      Inmos Libraries
--      Copyright (C) 1988, 1989, 1990, 1991, 1993  Inmos Limited
--
--      This library is free software; you can redistribute it and/or
--      modify it under the terms of the GNU Lesser General Public
--      License as published by the Free Software Foundation; either
--      version 2 of the License, or (at your option) any later version.
--
--      This library is distributed in the hope that it will be useful,
--      but WITHOUT ANY WARRANTY; without even the implied warranty of
--      MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
--      Lesser General Public License for more details.
--
--      You should have received a copy of the GNU Lesser General Public
--      License along with this library; if not, write to the Free Software
--      Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307  USA
--

--** @module forall

#IF (TARGET.BITS.PER.WORD = 16)

--{{{  #options and #comments
#OPTION "E V"  -- no compiler libs, no vector space
#COMMENT "Support routines library, V1.1 21/02/89"
#COMMENT "(c) Copyright INMOS Limited 1988"
--}}}

#PRAGMA TRANSLATE QUADSHIFTRIGHT "QUADSHIFTRIGHT%CHK"

#INCLUDE "realvals.inc"

--{{{  QUADSHIFTRIGHT -- T2
PROC QUADSHIFTRIGHT ([4]INT X.out, INT Guard.out, VAL [4]INT X.in,
                     VAL INT Guard.in, Places)

  -- Function version (10 Jun 87)

  INT carry, bits, sticky :
  [4] INT X.work :
  PLACE X.work IN WORKSPACE :
  SEQ
    X.work := X.in
    IF
      Places < BitsPerWord
        SEQ
          carry,  Guard.out := SHIFTRIGHT ( X.work[0],  Guard.in,  Places)
          carry,  X.out[0] := SHIFTRIGHT ( X.work[1],  X.work[0],  Places)
          carry,  X.out[1] := SHIFTRIGHT ( X.work[2],  X.work[1],  Places)
          X.out[3],  X.out[2] := SHIFTRIGHT ( X.work[3],  X.work[2], Places)
          sticky := Guard.in
      Places < (BitsPerWord * 2)
        SEQ
          bits := Places - BitsPerWord
          carry,  Guard.out := SHIFTRIGHT ( X.work[1],  X.work[0], bits)
          carry,  X.out[0] := SHIFTRIGHT ( X.work[2],  X.work[1],  bits)
          X.out[2],  X.out[1] := SHIFTRIGHT ( X.work[3],  X.work[2], bits)
          X.out[3] := 0
          sticky := Guard.in \/ X.work[0]
      Places < (BitsPerWord * 3)
        SEQ
          bits := Places - (BitsPerWord * 2)
          carry,  Guard.out := SHIFTRIGHT ( X.work[2],  X.work[1], bits)
          X.out[1],  X.out[0] := SHIFTRIGHT ( X.work[3],  X.work[2], bits)
          X.out[2] := 0
          X.out[3] := 0
          sticky := (Guard.in \/ X.work[0]) \/ X.work[1]
      Places < (BitsPerWord * 4)
        SEQ
          X.out[0],  Guard.out := SHIFTRIGHT ( X.work[3],  X.work[2], Places - (BitsPerWord * 3))
          X.out[1] := 0
          X.out[2] := 0
          X.out[3] := 0
          sticky := ((Guard.in \/ X.work[0]) \/ X.work[1]) \/ X.work[2]
      Places < (BitsPerWord * 5)
        SEQ
          carry,  Guard.out := SHIFTRIGHT ( 0,  X.work[3], Places - (BitsPerWord * 4))
          X.out[0] := 0
          X.out[1] := 0
          X.out[2] := 0
          X.out[3] := 0
          sticky := (((Guard.in \/ X.work[0]) \/ X.work[1]) \/ X.work[2]) \/
           X.work[3]
      TRUE
        SEQ
          X.out[0] := 0
          X.out[1] := 0
          X.out[2] := 0
          X.out[3] := 0
          Guard.out := 0
          sticky := (((Guard.in \/ X.work[0]) \/ X.work[1]) \/ X.work[2]) \/
           X.work[3]
    IF
      sticky <> 0
        Guard.out := Guard.out \/ 1
      TRUE
        SEQ
:
--}}}

#ENDIF
