--
--	Inmos Libraries
--	Copyright (C) 1988, 1989, 1990, 1991, 1993  Inmos Limited
--
--	This library is free software; you can redistribute it and/or
--	modify it under the terms of the GNU Lesser General Public
--	License as published by the Free Software Foundation; either
--	version 2 of the License, or (at your option) any later version.
--
--	This library is distributed in the hope that it will be useful,
--	but WITHOUT ANY WARRANTY; without even the implied warranty of
--	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
--	Lesser General Public License for more details.
--
--	You should have received a copy of the GNU Lesser General Public
--	License along with this library; if not, write to the Free Software
--	Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307  USA
--

--** @module forall

#IF (TARGET.BITS.PER.WORD = 32)

#IF DEFINED(TARGET.HAS.FPU)

--{{{  #options and #comments
#OPTION "E W V"  -- no compiler libs, full ASMs, no vector space
#COMMENT "T8 compiler library, V1.3 15/06/90"
#COMMENT "(c) Copyright INMOS Limited 1988"
--}}}

#PRAGMA TRANSLATE REAL64GT "REAL64GT%O"

--{{{  
BOOL FUNCTION REAL64GT (VAL REAL64 X, Y)

  --{{{  history
  -- V1.1, 2-Aug-89
  -- Port to new compiler ( oc ); V1.2 12-Feb-90 SRH
  -- Bug fix: floating point unit's error flag was not being left
  -- in its default state, so added FPUCLRERR; V1.3 15-Jun-90 SRH
  --}}}

  BOOL result:
  VALOF
    ASM
      LD ADDRESSOF X
      FPLDNLDB
      LD ADDRESSOF Y
      FPLDNLDB
      FPGT
      ST result
    RESULT result
:
--}}}

#ELSE -- !TARGET.HAS.FPU

--{{{  #options and #comments
#OPTION "V"  -- no vector space
#COMMENT "TA compiler library, V1.2 27/11/92"
#COMMENT "(c) Copyright INMOS Limited 1988"
--}}}

--{{{  #PRAGMA TRANSLATE
#PRAGMA TRANSLATE REAL64GT "REAL64GT%O"
--}}}

--{{{  
BOOL FUNCTION REAL64GT (VAL REAL64 X, Y)

  --{{{  history
  -- Beta-2 (27feb87)
  -- V1.1 21/02/89
  -- V1.2 27-Nov-92 SRH; bug fix TS/1962 to change behaviour when one or
  -- both arguments is NaN - NaNs should always compare FALSE.
  --}}}

  #INCLUDE "endian.inc"

  BOOL greater.than.if.ordered :
  VALOF
    --{{{  
    VAL [2]INT X RETYPES X :
    VAL [2]INT Y RETYPES Y :
    VAL low  IS lo :
    VAL high IS hi :
    INT MagX :
    INT MagY :
    SEQ
      MagX := X [high] /\ #7FFFFFFF
      MagY := Y [high] /\ #7FFFFFFF
      IF
        --{{{  X and Y have different signs
        (X [high] >< Y [high]) < 0       -- Still infinities but no NaNs
          --Signs are different hence test Y < 0 and not both 0
          greater.than.if.ordered := (Y [high] < 0) AND
                  ((((MagX PLUS MagY) \/ X [low]) \/ Y [low]) <> 0)
        --}}}
        --{{{  X and Y both negative
        X [high] < 0
          SEQ
            IF
              MagY = MagX
                --This is the equivalent of an unsigned comparison
                greater.than.if.ordered := (Y [low] >< #80000000) > (X [low] >< #80000000)
              TRUE
                greater.than.if.ordered := (MagY > MagX)
        --}}}
        --{{{  TRUE - X and Y both positive
        TRUE
          SEQ
            IF
              MagX = MagY
                --This is the equivalent of an unsigned comparison
                greater.than.if.ordered := (X [low] >< #80000000) > (Y [low] >< #80000000)
              TRUE
                greater.than.if.ordered := (MagX > MagY)
        --}}}
    --}}}
    RESULT (greater.than.if.ordered AND DORDERED(X, Y))
:
--}}}

#ENDIF -- !TARGET.HAS.FPU

#ENDIF


#IF (TARGET.BITS.PER.WORD = 16)

--{{{  #options and #comments
#OPTION "V"  -- no vector space
#COMMENT "T2 compiler library, V1.2 27/11/92"
#COMMENT "(c) Copyright INMOS Limited 1988"
--}}}

--{{{  #PRAGMA TRANSLATE
#PRAGMA TRANSLATE REAL64GT "REAL64GT%O"
--}}}

--{{{  
BOOL FUNCTION REAL64GT (VAL REAL64 X, Y)

  --{{{  history
  -- Beta-3 (11may87)  Function version (10 Jun 87)
  -- V1.1 21/02/89
  -- V1.2 27-Nov-92 SRH; bug fix TS/1962 to change behaviour when one or
  -- both arguments is NaN - NaNs should always compare FALSE.
  --}}}

  BOOL greater.than.if.ordered :
  VALOF
    VAL [4]INT X RETYPES X :
    VAL [4]INT Y RETYPES Y :
    INT MagX, MagY :
    VAL INT DRealInf.high IS #7FF0 :
    SEQ
      MagX := X [3] /\ #7FFF
      MagY := Y [3] /\ #7FFF
      IF
        --{{{  X and Y have different signs
        (X [3] >< Y [3]) < 0       -- Still infinities but no NaNs
          --Signs are different hence test Y < 0 and not both 0
          greater.than.if.ordered := (Y [3] < 0) AND
                   (((MagX PLUS MagY) \/
                    (((X [2] \/ X [1]) \/ X [0]) \/
                              ((Y [2] \/ Y [1]) \/ Y [0]))) <> 0)
        --}}}
        --{{{  X and Y both negative
        X [3] < 0
          INT Dump,Carry :
          SEQ
            Carry, Dump := LONGDIFF (X[0], Y[0], 0)
            Carry, Dump := LONGDIFF (X[1], Y[1], Carry)
            Carry, Dump := LONGDIFF (X[2], Y[2], Carry)
            Carry, Dump := LONGDIFF (MagX, MagY, Carry)
            greater.than.if.ordered := (Dump < 0)    -- i.e. Mag Y > Mag X
        --}}}
        --{{{  TRUE - X and Y both positive
        TRUE
          INT Dump,Carry :
          SEQ
            Carry, Dump := LONGDIFF (Y[0], X[0], 0)
            Carry, Dump := LONGDIFF (Y[1], X[1], Carry)
            Carry, Dump := LONGDIFF (Y[2], X[2], Carry)
            Carry, Dump := LONGDIFF (MagY, MagX, Carry)
            greater.than.if.ordered := (Dump < 0)    -- i.e. Mag X > Mag Y
        --}}}
    RESULT (greater.than.if.ordered AND DORDERED(X, Y))
:
--}}}

#ENDIF
